; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Commission Database"
#define MyAppVersion "2.0"
#define MyAppPublisher "ViaThinkSoft"
#define MyAppURL "https://www.github.com/danielmarschall/cmdb2/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
; Hinweis: Die geschweiften Klammern sind so genau richtig! {{...} , siehe jrsoftware.org/ishelp/index.php?topic=consts
AppId={{5766ca0a-2929-42e1-a5ec-6db5de59d326}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
;DefaultDirName=C:\HS-Service
DefaultDirName={autopf}\ViaThinkSoft\{#MyAppName} {#MyAppVersion}
DisableProgramGroupPage=yes
LicenseFile=license.rtf
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputBaseFilename=CmDb2_Setup
OutputDir=.
PrivilegesRequiredOverridesAllowed=dialog
UsePreviousPrivileges=no
ShowLanguageDialog=no
WizardStyle=modern
;WizardSmallImageFile=..\Private\Export Mini Graphic.bmp
SetupIconFile=..\CmDb2.ico
UninstallDisplayIcon={app}\CmDb2.exe
; 64 Bit Support:
ArchitecturesAllowed=x86compatible x64compatible
ArchitecturesInstallIn64BitMode=x64compatible
; Configure Sign Tool in InnoSetup at "Tools => Configure Sign Tools" (adjust the path to your SVN repository location)
; Name    = sign_single   
; Command = "D:\SVN\Delphi\DevTools\AuthentiCode Sign\sign_single.bat" $f
SignTool=sign_single
SignedUninstaller=yes

[Languages]
Name: en; MessagesFile: "compiler:Default.isl"

[Tasks]
; DM entfernt: Flags: unchecked
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

; Die Dinger werden von Delphi kompiliert
Source: "\\Win11Dev\C$\SVN\CMDB\trunk\CmDb2.exe"; DestDir: "{app}"; Flags: ignoreversion signonce
Source: "\\Win11Dev\C$\SVN\CMDB\trunk\CmDb2.64.exe"; DestDir: "{app}"; Flags: ignoreversion signonce; Check: Is64BitInstallMode

Source: "..\CurConv.dll"; DestDir: "{app}"; Flags: ignoreversion signonce
Source: "..\CurConv.64.dll"; DestDir: "{app}"; Flags: ignoreversion signonce; Check: Is64BitInstallMode
                                           
Source: "license.rtf"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\*.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\HelpStyle.css"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\DB\*"; DestDir: "{app}\DB"; Flags: ignoreversion recursesubdirs

[UninstallDelete]
Type: files; Name: "{app}\Setup.ini"

[Icons]
Name: "{autoprograms}\{#MyAppName} {#MyAppVersion}"; Filename: "{app}\CmDb2.64.exe"; Check: Is64BitInstallMode
Name: "{autoprograms}\{#MyAppName} {#MyAppVersion}"; Filename: "{app}\CmDb2.exe"; Check: not Is64BitInstallMode

Name: "{autodesktop}\{#MyAppName} {#MyAppVersion}"; Filename: "{app}\CmDb2.64.exe"; Tasks: desktopicon; Check: Is64BitInstallMode
Name: "{autodesktop}\{#MyAppName} {#MyAppVersion}"; Filename: "{app}\CmDb2.exe"; Tasks: desktopicon; Check: not Is64BitInstallMode

[Run]
Filename: "{app}\CmDb2.64.exe"; Description: "Run {#MyAppName} {#MyAppVersion}"; Flags: postinstall nowait skipifsilent unchecked; Check: Is64BitInstallMode
Filename: "{app}\CmDb2.exe"; Description: "Run {#MyAppName} {#MyAppVersion}"; Flags: postinstall nowait skipifsilent unchecked; Check: not Is64BitInstallMode

[Code]

procedure CurStepChanged(CurStep: TSetupStep);
var
  ownHash: string;
begin
  if CurStep=ssPostInstall then
  begin
    ownHash := GetMD5OfFile(ExpandConstant('{srcexe}'));

    // Wir merken uns den MD5-Hash vom installierten Setup, sodass wir wissen,
    // ob das Update OK war und wann wir wieder ein neues Update brauchen
    SetIniString('Setup', 'InstalledMD5', ownHash, ExpandConstant('{app}\Setup.ini'));
  end;
end;